#compdef logclean
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S : \
'(* -)'{'--help','-h'}'[print a brief help]' \
'(* -)'{'--man','-\\?'}'[show extended help as a manpage]' \
'(* -)'{'--version','-V'}'[print version]' \
{'(--oldformat)-O','(-O)--oldformat'}'[logfiles are in old format \<portage-2.1.1]' \
{'(--pretend -p '{'--show)-s','-s)--show'},'(--show -s '{'--pretend)-p','-p)--pretend'}}'[only show what would be removed]' \
{'(--root)-r+','(-r)--root='}'[let specified directory be root]:root directory:_files -/' \
{'(--times)-t+','(-t)--times='}'[keep specified number of emerges in emerge.log]:emerge times to keep:(5)' \
{'(--keep)-k+','(-k)--keep='}'[keep specified number of portage calls in emerge.log]:portage calls to keep:(100)' \
{'(--older)-o+','(-o)--older='}'[only delete files older than specified hours]:hours:(1)' \
{'(--newer)-n+','(-n)--newer='}'[skip scanning older than specified hours]:hours to skip:(1)' \
{'(--keepcolor)-C','(-C)--keepcolor'}'[do not remove color sequences]' \
{'(--xz --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)-x','(-x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)--xz'}'[use xz as compression program]' \
{'(--xz -x --lzma --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)-l','(--xz -x -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)--lzma'}'[use lzma as compression program]' \
{'(--xz -x --lzma -l --zstd --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)-z','(--xz -x --lzma -l -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)--zstd'}'[use zstd as compression program]' \
{'(--xz -x --lzma -l --zstd -z --brotli --lz4 -4 --bzip2 -b --gzip -g --compprog -c)-B','(--xz -x --lzma -l --zstd -z -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)--brotli'}'[use brotli as compression program]' \
{'(--xz -x --lzma -l --zstd -z --brotli -B --lz4 --bzip2 -b --gzip -g --compprog -c)-4','(--xz -x --lzma -l --zstd -z --brotli -B -4 --bzip2 -b --gzip -g --compprog -c)--lz4'}'[use lz4 as compression program]' \
{'(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 --gzip -g --compprog -c)-b','(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 -b --gzip -g --compprog -c)--bzip2'}'[use bzip2 as compression program]' \
{'(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip --compprog -c)-g','(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog -c)--gzip'}'[use gzip as compression program]' \
{'(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g --compprog)-c+','(--xz -x --lzma -l --zstd -z --brotli -B --lz4 -4 --bzip2 -b --gzip -g -c)--compprog='}'[use specified compression program]:compression-program:_command_names -e' \
'*'{'-a+','--comparg='}'[add specified compression arg]:compression option:()' \
{'(--compargs)-A+','(-A)--compargs='}'[use specified compression args as default]:default compression options:()' \
{'(--ignore)-i','(-i)--ignore'}'[ignore installed packages for emerge.log]' \
{'(--debug -d)-D','(--debug -D)-d','(-d -D)--debug'}'[before deleting print a reason. emerge.log to stdout]' \
'1:what to clean:->cmds' \
'*::logfile:_files'
local ret=$?
case $state in
(cmds)
	local cmds
	cmds=(
	'all:Do all cleanups\: emerge log elog notices cache revdep builds'
	'emerge:Cleanup /var/log/emerge.log'
	'log:Remove unused files in $PORTAGE_LOGDIR (/var/log/portage)'
	'logs:Remove unused files in $PORTAGE_LOGDIR (/var/log/portage)'
	'elog:Remove unused files in $PORTAGE_LOGDIR/elog'
	'notices:Remove unused files in $PORT_ENOTICE_DIR'
	'notes:Remove unused files in $PORT_ENOTICE_DIR'
	'cache:Remove unneeded data in /var/cache/edb/dep'
	'revdep:Remove unneeded data in /var/cache/revdep-rebuild'
	'builds:Remove unneeded data in $PORT_TMPDIR/portage (/var/tmp/portage)'
	'shorten:shorten logfiles FILE1 FILE2 ...'
	'uncolor:uncolor logfiles FILE1 FILE2 ...'
	'help:print brief help'
	'man:print verbose manpage'
	)
	_describe -t commands 'what to clean' cmds && ret=0;;
esac
return ret
